buildscript {
	ext {
		springBootVersion = '1.0.0.RC3'
	}
	repositories {
		mavenLocal()
		maven { url "http://repo.springsource.org/libs-snapshot" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'

mainClassName = "simpleblog.Application"

ext.sourceEncoding = "UTF-8"

repositories {
	mavenCentral()
	maven { url "http://repo.springsource.org/libs-snapshot" }
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-data-jpa")
	compile("org.springframework.boot:spring-boot-starter-security")
	compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	compile("org.springframework.boot:spring-boot-starter-logging")
	compile("org.apache.tomcat.embed:tomcat-embed-jasper")
	compile("org.hibernate:hibernate-validator")
	compile("org.hsqldb:hsqldb")
	compile("commons-lang:commons-lang:2.6")
	compile("org.webjars:bootstrap:3.1.0")
	compile("org.webjars:jquery:2.1.0-2")
}

task wrapper(type: Wrapper) { gradleVersion = '1.11' }

// fix encoding issue in gradle eclipse plugin
eclipseJdt << {
  File resourcesPrefsFile = file('.settings/org.eclipse.core.resources.prefs')
  resourcesPrefsFile.write('eclipse.preferences.version=1\n')
  resourcesPrefsFile.append("encoding/<project>=${project.sourceEncoding}")
}

// fix source code support in wtp
eclipse {
	classpath {
	  containers.remove("org.eclipse.jdt.launching.JRE_CONTAINER")
	  containers.add("org.eclipse.jdt.launching.JRE_CONTAINER")
	  file {
		withXml { xml ->
		  def node = xml.asNode()
		  def webContainer = node.find { 'org.eclipse.jst.j2ee.internal.web.container'  == it.@path }
		  if(webContainer) {
			node.remove(webContainer)
			node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
		  }
		}
	  }
	}
  }